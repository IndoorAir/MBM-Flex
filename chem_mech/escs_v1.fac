* ****************************************************************** ;
* Extremely Simplified Chemical Scheme (ESCS) - version 1            ;
*                                                                    ;
* All reactions and rate constants are taken from the MCM v3.3.1,    ;
* unless otherwise stated.                                           ;
* Complex and simple (generic) rate coefficients are calculated      ;
* according to the MCM v3.3.1 protocol.                              ;
*                                                                    ;
* R. Sommariva - September 2024                                      ;
*                                                                    ;
* ****************************************************************** ;
*;
* Variable definitions.  All species are listed here.;
*;
VARIABLE
 O3 NO NO2 OH HO2 CO VOC RRO2 RRNO3 RRCHO ;
****************************************************** ;
*;
* Generic Rate Coefficients ;
*;
JJ1 = 3e-5 ;
JJ4 = 8e-3 ;
*;
* Complex reactions ;
*;
KMT05 = 1.44D-13*(1+(M/4.2D+19)) ;
*;
K80 = 3.2D-30*M*(TEMP/300)@-4.5 ;
K8I = 3.0D-11 ;
KR8 = K80/K8I ;
FC8 = 0.41 ;
NC8 = 0.75-1.27*(LOG10(FC8)) ;
F8 = 10@(LOG10(FC8)/(1+(LOG10(KR8)/NC8)**2)) ;
KMT08 = (K80*K8I)*F8/(K80+K8I) ;
*;
* ****************************************************************** ;
* Peroxy radicals. ;
*;
RO2 = RRO2 ;
*;
* ****************************************************************** ;
* Reaction definitions. ;
*;
% 1.40D-12*EXP(-1310/TEMP) : NO + O3 = NO2 ;
% JJ4 : NO2 = NO + O3 ;
% JJ1 : O3 = OH + OH ;
% KMT05 : OH + CO = HO2 ;
% 2.03D-16*(TEMP/300)@4.57*EXP(693/TEMP) : HO2 + O3 = OH ;
% 3.45D-12*EXP(270/TEMP) : HO2 + NO = OH + NO2 ;
% KMT08 : OH + NO2 = RRNO3 ;
% 7.6D-12*EXP(-585/TEMP) : OH + VOC = RRO2 ;
% 2.7D-12*EXP(360/TEMP)*0.05 : RRO2 + NO = RRNO3 ;
% 2.7D-12*EXP(360/TEMP)*0.95 : RRO2 + NO = RRCHO ;
*;
* End of Subset.  No. of Species = 10, No. of Reactions = 10 ;
